buildscript {
    ext {
        springBootVersion = '2.2.6.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'at.hollander'
sourceCompatibility = 1.13
version = System.getenv('IBEX_BUILD_GITHUB_SHA') ?: '0000000000000000000000000000000000000000'

ext {
    speed = project.hasProperty('speed') ? project.getProperty('speed') : false
    offlineCompile = new File("$buildDir/output/lib")
}

bootJar {
    archiveFileName = 'ibex.jar'
    manifest {
        attributes 'Implementation-Title': 'Ibex Backend',
                'Implementation-Version': System.getenv('IBEX_BUILD_GITHUB_SHA') ?: '0000000000000000000000000000000000000000',
                'Ibex-Build-Github-Sha': System.getenv('IBEX_BUILD_GITHUB_SHA') ?: '0000000000000000000000000000000000000000',
                'Ibex-Build-Github-Ref': System.getenv('IBEX_BUILD_GITHUB_REF') ?: 'no_ref_specified',
                'Ibex-Build-Github-Tag': System.getenv('IBEX_BUILD_GITHUB_TAG') ?: 'no_tag_specified',
                'Ibex-Build-Github-Run-Id': System.getenv('IBEX_BUILD_GITHUB_RUN_ID') ?: '0',
                'Ibex-Build-Github-Run-Number': System.getenv('IBEX_BUILD_GITHUB_RUN_NUMBER') ?: '0'
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

dependencies {
    if (speed) {
        implementation fileTree(dir: offlineCompile, include: '*.jar')
    } else {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'
        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

        implementation('com.google.guava:guava:28.2-jre')
        implementation('commons-io:commons-io:2.6')
        implementation('org.jooq:jool:0.9.14')

        implementation('com.itextpdf:itextpdf:5.5.13.1')

        runtimeOnly('mysql:mysql-connector-java:8.0.19')
        implementation('redis.clients:jedis:3.2.0')
        implementation('org.flywaydb:flyway-core:6.3.2')

        implementation('org.springframework.boot:spring-boot-starter-data-jpa')
        implementation('org.springframework.boot:spring-boot-starter-security')
        implementation('org.springframework.boot:spring-boot-starter-validation')
        implementation('org.springframework.boot:spring-boot-starter-web')
        testImplementation('org.springframework.boot:spring-boot-starter-test')

        implementation('org.springframework.session:spring-session-core:2.2.2.RELEASE')
        implementation('org.springframework.session:spring-session-jdbc:2.2.2.RELEASE')
        implementation('org.springframework.session:spring-session-data-redis:2.2.2.RELEASE')
        implementation('com.jcabi:jcabi-manifests:1.1')
    }
}

task downloadRepos(type: Copy) {
    from configurations.compile
    from configurations.compileOnly
    from configurations.runtime
    from configurations.testCompile
    into offlineCompile
}
